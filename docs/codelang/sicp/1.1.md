# 1.1 程序设计的基本元素


## 回顾

- 基本单元：最简单的个体。
- 组合：从简单的个体出发构造出复合的对象。
- 抽象：为复合的对象命名，视为基本单元操作。

## E1.1

```scheme
10 ; 10
(+ 5 3 4) ; 12
(- 9 1) ; 8
(/ 6 2) ; 3
(+ (* 2 4) (- 4 6)) ; 6
(define a 3) ; a = 3
(define b (+ a 1)) ; b = 4
(+ a b (* a b)) ; 19
(= a b) ; false

(if (and (> b a) (< b (* a b)))
    b
    a)
; 4

(cond ((= a 4) 6)
      ((= b 4) (+ 6 7 a))
      (else 25))
; 16
    
(+ 2 (if (> b a) b a))
; 6

(* (cond ((> a b) a)
         ((< a b) b)
         (else -1))
   (+ a 1))
; 16
```

## E1.2

$\frac{5+4+(2-(3-(6+\frac{4}{5})))}{3(6-2)(2-7)}$，换成前缀表达式

```scheme
(/ (+ 5 4
      (- 2 
         (- 3 
            (+ 6 (/ 4 5))))) 
   (* 3 (- 6 2) (- 2 7)))
; -37/150
```

## E1.3

两个较大数之和 -> 减去最小的数，  

```scheme
(define (min-value a b c)
    (cond ((and (< a b) (< a c)) a)
          ((and (< b a) (< b c)) b)
          ((and (< c a) (< c b)) c))
)

(define (two-bigger-sum a b c)
    (+ a b c 
        (- 0 (min-value a b c))
    )
)
```

## E1.4

```scheme
(define (a-plus-abs-b a b)
    ((if (> b 0) + -) a b)
)
```
`if` 这里是将过程（+ -）作为返回值，将过程视为基本元素来操纵，是高阶过程的特征。 


## E1.5

```scheme
(define (p)
    (p))

(define (test x y)
  (if (= x 0)
      0
      y))

(test 0 (p))
```

p 的定义是无限循环。如果是应用序，先对参数求值，再进行下一轮代换，这里会对 p 求值，从而陷入无限循环；如果是正则序，参数先不求值，进行一一轮代换，会在 `if` 语句中，因 `x = 0`，返回 `0` 而结束。

应用序的特点是，这样一来不用对参数重复计算，DrRacket 解释器采用的是应用序。

## E1.6

```scheme
(define (new-if predicate then-clause else-clause)
    (cond (predicate then-clause)
          (else else-clause)))
```

再来回顾一下 `if` 这个特殊过程求值规则的定义，其中的谓词部分先行求值，根据其结果<mark>确定</mark>随后求值的子表达式部分。一个子表达式会被求值，另一个则不会。如果通过 `cond` 定义为一个常规过程，两个子表达式都会被求值，无论谓词部分的结果是真是假。

```scheme
(if #t (display " true ") (display " false "))     ; true
(new-if #t (display " true ") (display " false ")) ; true false
```

回到求平方根的程序：
```scheme
(define (sqrt x)
  (define (square x)
    (* x x))
  (define (average a b)
    (/ (+ a b) 2))
  (define (good-enough? guess)
    (< (abs (- (square guess) x)) 0.001))
  (define (improve guess)
    (average guess (/ x guess)))
  (define (sqrt-iter guess)
    (if (good-enough? guess)              ; if -> new-if
        guess
        (sqrt-iter (improve guess))))
  (sqrt-iter 1.0))
```
如果替换成 `new-if` 的话，即使 `guess` `good-enough` 了，`sqrt-iter` 仍会继续，无限递归到内存耗尽而报错。


## E1.7

对于足够小的数的平方根而言，`good-enough?` 的精度是不够的，先找到这样足够小的数看下结果，
```
> (sqrt 0.01)
0.10032578510960605
> (sqrt 0.00001)
0.03135649010771716
```

按照给出的建议，`good-enough` 改为猜测值的变化量相比猜测值的比率足够小，

```scheme
(define (good-enough? guess)
  (< (/ (abs (- (improve guess) guess)) guess) 0.001))
; > (sqrt 0.01)
; 0.10000052895642693
; > (sqrt 0.00001)
; 0.0031622926477232706
```

精度确实提升了不少。


## E1.8

求立方根的牛顿法，猜测值的 `improve` 过程为 $\frac{\frac{x}{y^{2}} + 2y}{3}$，

```scheme
(define (cbrt x)
  (define (cube x)
    (* x x x))
  (define (improve guess x)
    (/ (+ (/ x (* guess guess)) (* 2 guess)) 3))
  (define (good-enough? guess x)
    (< (abs (- (cube guess) x)) 0.001))
  (define (cbrt-iter guess x)
    (if (good-enough? guess x)
        guess
        (cbrt-iter (improve guess x) x)))
  (cbrt-iter 1 x))

(display (exact->inexact (cbrt (* 4 4 4)))) ；4.000017449510739
(display (exact->inexact (cbrt (* 5 5 5)))) ；5.000000000287929
```

这个效率不太行，每加一个单位值，计算时间呈指数上升。

